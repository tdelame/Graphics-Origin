# version 440
layout(quads, fractional_odd_spacing, ccw) in;
in vec2 tes_texcoord[];

out vec2 tex_coordinates;
out vec3 world_coordinates;

uniform sampler2D terrain;
uniform mat4 mvp;
uniform float inv_radius;

void main(){
    float u = gl_TessCoord.x;
    float v = gl_TessCoord.y;

    vec4 a = mix( 
       vec4( gl_in[1].gl_Position.xy, tes_texcoord[1] ), 
       vec4( gl_in[0].gl_Position.xy, tes_texcoord[0] ),
       u );
    vec4 b = mix( 
       vec4( gl_in[2].gl_Position.xy, tes_texcoord[2] ),
       vec4( gl_in[3].gl_Position.xy, tes_texcoord[3] ),
       u );
	vec4 c = mix( a, b, v );
	
	tex_coordinates = c.ba;
	world_coordinates = vec3( c.rg, texture( terrain, tex_coordinates).a );
	
	gl_Position = mvp * vec4( world_coordinates, 1.0 );
}